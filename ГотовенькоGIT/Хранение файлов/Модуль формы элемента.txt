&НаКлиенте
Перем ВремяСозданияФайла;

&НаСервере
// Процедура извлекает данные объекта из временного хранилища, 
// производит модификацию элемента справочника и записывает его.
// 
// Параметры: 
//  АдресВременногоХранилища – Строка – адрес временного хранилища. 
//
Процедура ПоместитьФайлОбъекта(АдресВременногоХранилища)
	ЭлементСправочника = РеквизитФормыВЗначение("Объект");
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	ЭлементСправочника.ДанныеФайла = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());
	Файл = Новый Файл(ЭлементСправочника.ИмяФайла);
	ЭлементСправочника.ИмяФайла = Файл.Имя;
	ЭлементСправочника.Наименование = СтрРазделить(Файл.Имя, ".",)[0];
	ЭлементСправочника.Записать();
	Модифицированность = Ложь;
	УдалитьИзВременногоХранилища(АдресВременногоХранилища);
	ЗначениеВРеквизитФормы(ЭлементСправочника, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлСДиска(Команда)
	Перем ВыбранноеИмя;
	Перем АдресВременногоХранилища;
	
	НовыйОбъект = Объект.Ссылка.Пустая();
	Если ПоместитьФайл(АдресВременногоХранилища, "", ВыбранноеИмя, Истина) Тогда		
		Объект.ИмяФайла = ВыбранноеИмя;
		ПоместитьФайлОбъекта(АдресВременногоХранилища);
		Если НовыйОбъект Тогда
			ОтобразитьИзменениеДанных(Объект.Ссылка, ВидИзмененияДанных.Добавление);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьДвоичныйФайл()
	Возврат РеквизитФормыВЗначение("Объект").ДанныеФайла.Получить();
КонецФункции

&НаКлиенте
Процедура СохранитьФайл(Команда)
	Если ПустаяСтрока(Объект.ИмяФайла) Тогда 
		Сообщить("Пустое имя файла");
		Возврат 
	КонецЕсли;
	Диалог = новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = "Любой файл, *.*|*.*";
	
	Диалог.ПолноеИмяФайла = Объект.ИмяФайла;
	Если Диалог.Выбрать() Тогда
		ИмяФайла = Диалог.ПолноеИмяФайла;
		ДвоичныйФайл = ПолучитьДвоичныйФайл();
		ДвоичныйФайл.Записать(ИмяФайла);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	Если ПустаяСтрока(Объект.ИмяФайла) Тогда 
		Сообщить("Пустое имя файла");
		Возврат 
	КонецЕсли;
	ДвоичныйФайл = ПолучитьДвоичныйФайл();
	ПутьКФайлу = КаталогВременныхФайлов() + Объект.ИмяФайла;
	ДвоичныйФайл.Записать(ПутьКФайлу);
	ЗапуститьПриложение(ПутьКФайлу);
	мФайл = Новый Файл(ПутьКФайлу);
	ВремяСозданияФайла = мФайл.ПолучитьВремяИзменения();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	Если ПустаяСтрока(Объект.ИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = КаталогВременныхФайлов() + Объект.ИмяФайла;	
	Файлик = Новый Файл(ПутьКФайлу);
	
	Если ПустаяСтрока(ВремяСозданияФайла) Тогда 				    		// Если мы не Открывали файл 
		Возврат;
	КонецЕсли;
	
	Попытка
		ДвоичныйФайл = Новый ДвоичныеДанные(ПутьКФайлу);                	// Если файл занят
	Исключение
		Возврат;
	КонецПопытки;
	
	Если ВремяСозданияФайла = Файлик.ПолучитьВремяИзменения() Тогда       	// Если открыли, но ничего не изменили
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Вопрос("Файл " + Объект.ИмяФайла + " был модифицирован. Сохранить изменения в базе?", Режим, 0);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Адрес = ПоместитьВоВременноеХранилище(ДвоичныйФайл, УникальныйИдентификатор);
		ПоместитьФайлОбъекта(Адрес);		
	КонецЕсли;
КонецПроцедуры