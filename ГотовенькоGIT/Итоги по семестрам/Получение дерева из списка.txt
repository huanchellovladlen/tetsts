#Область Формирования_и_оформления_итогов_по_семестрам

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если  ТекущаяСтраница.Имя="ГруппаИтогиПоСеместрам" Тогда
		ПолучитьИтогиНаСервере(Объект.Ссылка);
	КонецЕсли;

	КоллекцияЭлементовДерева = ИтогиПоСеместрам.ПолучитьЭлементы();
	Для Каждого Строка Из КоллекцияЭлементовДерева Цикл    
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Элементы.ИтогиПоСеместрам.Развернуть(ИдентификаторСтроки);
	КонецЦикла;	
КонецПроцедуры
	
&НаСервере
Процедура ПолучитьИтогиНаСервере(Ссылка)
	ТЗ = Объект.Дисциплины.Выгрузить();
	Запрос = Новый Запрос;
	// Содержимое объекта данных может быть выбрано только во временную таблицу. Поэтому создаётся пакетный запрос
	// Колонки результата запроса должны соответствовать колонкам дерева на формее 
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПланыСпециальностей.Сем,
		|	ПланыСпециальностей.Дисциплина,
		|	ПланыСпециальностей.ЛекцииУстанСессия,
		|	ПланыСпециальностей.ЛБУстанСессия,
		|	ПланыСпециальностей.ПЗУстанСессия,
		|	ПланыСпециальностей.Лекции,
		|	ПланыСпециальностей.ЛБ,
		|	ПланыСпециальностей.ПЗ,
		|	ПланыСпециальностей.Всего,
		|	ПланыСпециальностей.КурсПроект,
		|	ПланыСпециальностей.ВсегоЧасов,
		|	ПланыСпециальностей.КурсРаб,
		|	ПланыСпециальностей.КонтрРаб,
		|	ПланыСпециальностей.ТипР,
		|	ПланыСпециальностей.РасР,
		|	ПланыСпециальностей.Зачет,
		|	ПланыСпециальностей.Экз
		|ПОМЕСТИТЬ ВременнаяТаблица
		|ИЗ
		|	&ТЗ КАК ПланыСпециальностей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПланыСпециальностей.Сем КАК Семестр,
		|	ПланыСпециальностей.Дисциплина КАК Дисциплина,
		|	ПланыСпециальностей.ЛекцииУстанСессия КАК ЛкУст,
		|	ПланыСпециальностей.ЛБУстанСессия КАК ЛБУст,
		|	ПланыСпециальностей.ПЗУстанСессия КАК ПЗУст,
		|	ПланыСпециальностей.Лекции КАК ЛК,
		|	ПланыСпециальностей.ЛБ КАК ЛБ,
		|	ПланыСпециальностей.ПЗ КАК ПЗ,
		|	ПланыСпециальностей.Всего КАК ВсегоАуд,
		|	ПланыСпециальностей.КурсПроект КАК КП,
		|	ПланыСпециальностей.ВсегоЧасов КАК ВсегоЧасов,
		|	ПланыСпециальностей.КурсРаб КАК КР,
		|	ПланыСпециальностей.КонтрРаб КАК КонтрРаб,
		|	ПланыСпециальностей.ТипР КАК ТР,
		|	ПланыСпециальностей.РасР КАК РР,
		|	ПланыСпециальностей.Зачет КАК Зачет,
		|	ПланыСпециальностей.Экз КАК Экзамен
		|ИЗ
		|	ВременнаяТаблица КАК ПланыСпециальностей
		|ИТОГИ
		|	СУММА(ЛкУст),
		|	СУММА(ЛБУст),
		|	СУММА(ПЗУст),
		|	СУММА(ЛК),
		|	СУММА(ЛБ),
		|	СУММА(ПЗ),
		|	СУММА(ВсегоАуд),
		|	СУММА(КП),
		|	СУММА(ВсегоЧасов),
		|	СУММА(КР),
		|	СУММА(КонтрРаб),
		|	СУММА(ТР),
		|	СУММА(РР),
		|	СУММА(Зачет),
		|	СУММА(Экзамен)
		|ПО
		|	ОБЩИЕ,
		|	Семестр";	
	Запрос.УстановитьПараметр("ТЗ", ТЗ);
	ПромДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ЗначениеВРеквизитФормы(ПромДерево, "ИтогиПоСеместрам");
	
	// Добавляем элемент
	Элемент = УсловноеОформление.Элементы.Добавить();
	// Определяем условия сравнения для Правой части и цвета оформления	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.СветлоЖелтыйЗолотистый);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Коричневый);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,Истина));
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИтогиПоСеместрам.Семестр");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено; 
	ОтборЭлемента.ПравоеЗначение = Истина; 
	ОтборЭлемента.Использование = Истина;
	// Определяем поля оформления через реквизит формы
	Для каждого Колонка Из ПромДерево.Колонки Цикл
		ПолеОформления = "ИтогиПоСеместрам" + Колонка.Имя;  
		ОформляемыеПоля = Элемент.Поля.Элементы.Добавить();
		ОформляемыеПоля.Поле = Новый ПолеКомпоновкиДанных(ПолеОформления);
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура РасчетИтоговПоСеместру(Команда)
	РасчетИтоговПоСеместруНаСервере();
КонецПроцедуры

&НаСервере
Процедура РасчетИтоговПоСеместруНаСервере()
	Если Элементы.ДисциплиныСбросСеместров.Заголовок = "«семестры»" Тогда
		Возврат;
	КонецЕсли;
	Сем = Справочники.Семестры.НайтиПоНаименованию(СтрЗаменить(Элементы.ДисциплиныСбросСеместров.Заголовок, "сем.", ""));
	Отбор = Новый Структура;
	Отбор.Вставить("Сем", Сем);
	СтрокаИтогов = Объект.Дисциплины.Выгрузить(Отбор);
	СтрокаИтогов.Свернуть("Сем,ЧислоНедель", "Всего,ВсегоЧасов,ЗачЕдиницы,КурсРаб,КурсПроект,КонтрРаб,ТипР,РасР,Зачет,Экз");
	
	ИтогСеместр = СтрокаИтогов[0].Сем.СеместрПрописью;
	ИтогАудЧасов = Формат(СтрокаИтогов[0].Всего / СтрокаИтогов[0].ЧислоНедель, "ЧЦ=8; ЧДЦ=1");
	ИтогЗачЕд = СтрокаИтогов[0].ЗачЕдиницы;
	ИтогКонтрРаб = СтрокаИтогов[0].КонтрРаб;
	ИтогКР = СтрокаИтогов[0].КурсРаб;
	ИтогКП = СтрокаИтогов[0].КурсПроект;
	ИтогРР = СтрокаИтогов[0].РасР;
	ИтогТР = СтрокаИтогов[0].ТипР;
	ИтогЗачет = СтрокаИтогов[0].Зачет;
	ИтогЭкз = СтрокаИтогов[0].Экз;
КонецПроцедуры

#КонецОбласти