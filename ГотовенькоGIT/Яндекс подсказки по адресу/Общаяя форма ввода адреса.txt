// Как запустить форму:
//
//		Парам = Новый Структура("Страна, ТипНаселенногоПункта, НаселенныйПункт, ТипУлицы, Улица, Дом, Корпус, Квартира",
//			Объект.Страна, Объект.ТипНаселенногоПункта, Объект.НаселенныйПункт, Объект.ТипУлицы, 
//			Объект.Улица, Объект.Дом, Объект.Корпус, Объект.Квартира);
//		Оповещение = Новый ОписаниеОповещения("ОбработкаАдресаЗавершение", ЭтотОбъект);
//		ОткрытьФорму("ОбщаяФорма.ВводАдреса", Парам,,,,,Оповещение,);

// При этом необходимо описать метод для обработки результата:
//
//		&НаКлиенте
//		Процедура ОбработкаАдресаЗавершение(Результат, ДополнительныеПараметры) Экспорт
//			Если ТипЗнч(Результат) = Тип("Структура") Тогда
//				Объект.Страна = Результат.Страна;
//				//..//
//				Объект.Квартира = Результат.Квартира;
//			КонецЕсли;
//		КонецПроцедуры

// тр-т Логойский тракт
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Страна) Тогда
		Страна = Параметры.Страна;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ТипНаселенногоПункта) Тогда
		ТипНаселенногоПункта = Параметры.ТипНаселенногоПункта;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.НаселенныйПункт) Тогда
		НаселенныйПункт = Параметры.НаселенныйПункт;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ТипУлицы) Тогда
		ТипУлицы = Параметры.ТипУлицы;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Улица) Тогда
		Улица = Параметры.Улица;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Корпус) Тогда
		Корпус = Параметры.Корпус;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Дом) Тогда
		Дом = Параметры.Дом;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Квартира) Тогда
		Квартира = Параметры.Квартира;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ПочтовыйИндекс) Тогда
		ПочтовыйИндекс = Параметры.ПочтовыйИндекс;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоискАдресаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	Если НЕ ЗначениеЗаполнено(Текст) Тогда
		Возврат; 
	КонецЕсли;
	
	Попытка 		// Отпрааляем запрос на определение улицы
		Соединение = Новый HTTPСоединение("suggest-maps.yandex.by",,,,,, Новый ЗащищенноеСоединениеOpenSSL);
		HTTPЗапрос = Новый HTTPЗапрос();	
		HTTPЗапрос.АдресРесурса = "/suggest-geo?&lang=ru_RU&outformat=json&part=" + Текст + "&v=7";
		ОтветHTTP = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
		Если ОтветHTTP.КодСостояния <> 200 Тогда
			Возврат;
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	
	Попытка			// Читаем ответ яндекс карт. Это не API, поэтому может сломаться в любой момент.
		ОтветСервера = ОтветHTTP.ПолучитьТелоКакСтроку();
		JSON = Новый ЧтениеJSON;
		JSON.УстановитьСтроку(ОтветСервера);
		jsonОтвет = ПрочитатьJSON(JSON);
		Для Каждого найденныйАдрес ИЗ jsonОтвет.results Цикл
			Если найденныйАдрес.kind = "street" Или найденныйАдрес.kind = "house" Тогда
				мГород = СтрРазделить(найденныйАдрес.desc, ",", Ложь);
				Подсказка = СокрЛП(мГород[мГород.ВГраница()]);
				Индекс = мГород.Количество() - 2;
				Пока Индекс >= 0 Цикл
					Подсказка = Подсказка + ", " + СокрЛП(мГород[Индекс]);
					Индекс = Индекс - 1;
				КонецЦикла;
				
				ДанныеВыбора.Добавить(Подсказка + "; " + найденныйАдрес.name);
			КонецЕсли;
		КонецЦикла;
	Исключение
		Возврат;
	КонецПопытки;
КонецПроцедуры

#Область ПопыткаРазделитьАдрес

&НаКлиенте
Процедура РазложитьАдрес(Команда)
	Если ПоискАдреса = "" Тогда
		Возврат;
	КонецЕсли;
	// ';' разделяет глобальную часть адреса (от страны до города) и локальную (от улицы до дома)
	СтранаИАндрес = СтрРазделить(ПоискАдреса, ";", Ложь);
	Если СтранаИАндрес.Количество() <> 2 Тогда
		Возврат;
	КонецЕсли;
	
	// Глобальная часть адреса. Пример: "Беларусь, Папернянский сельсовет, Минский район, деревня Сёмков Городок   
	мГлобАдрес = СтрРазделить(СтранаИАндрес[0], ",", Ложь);
	Если мГлобАдрес.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	Страна = ПолучитьСтрануИзСправочника(мГлобАдрес[0]);					
	мТипИИмя = РазделитьТипИИмяПосёлка(мГлобАдрес[мГлобАдрес.ВГраница()]);
	ТипНаселенногоПункта = мТипИИмя[0];
	НаселенныйПункт = мТипИИмя[1];
	
	// Локальная часть адреса. Пример: "Коммунистическая улица, 8"
	мУлицаИДом = СтрРазделить(СтранаИАндрес[1], ",", Ложь);
	Если мУлицаИДом.Количество() > 0 Тогда
		ТипИАдрес = РазложитьАдресНаКомпоненты(мУлицаИДом[0]);
		ТипУлицы = ТипИАдрес[0];
		Улица = ТипИАдрес[1];
		Если мУлицаИДом.Количество() > 1 Тогда
			мДомИКорпус = СтрРазделить(мУлицаИДом[1], "к");
			Если мДомИКорпус.Количество() > 1 Тогда
				Корпус = СокрЛП(мДомИКорпус[1]);
			Иначе
				Корпус = "0";
			КонецЕсли;
			Дом = СокрЛП(мДомИКорпус[0]);
			Если мУлицаИДом.Количество() > 2 Тогда
				Квартира = СокрЛП(мУлицаИДом[2]);
			Иначе
				Квартира = 0;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли
КонецПроцедуры

&НаКлиенте
Функция РазложитьАдресНаКомпоненты(АдресСТипом)
	// Разложим на название и тип. Тип может быть как спереди, так и сзади
	АдресСТипом = СокрЛП(АдресСТипом);
	мАдрес = СтрРазделить(АдресСТипом, " ", Ложь);
	мТипУлицы = Новый Массив(1);
	
	// Проверим 1-й и n-й элементы массива. Если нашли строчную букву, то это тип.
	// Добавляем тип улицы в мТип и удаляем его из мАдресСТипом
	КодТипаУлицы = КодСимвола(мАдрес[0], 1);
	Если КодТипаУлицы >= 1072 И КодТипаУлицы <= 1103 Тогда  	// а-я С 1072 По 1103
		мТипУлицы.Добавить(мАдрес[0]);
		мАдрес.Удалить(0);
	Иначе
		КодТипаУлицы = КодСимвола(мАдрес[мАдрес.ВГраница()], 1);
		Если КодТипаУлицы >= 1072 И КодТипаУлицы <= 1103 Тогда
			мТипУлицы.Добавить(мАдрес[мАдрес.ВГраница()]);
			мАдрес.Удалить(мАдрес.ВГраница());	
		КонецЕсли;
	КонецЕсли;
	
	мДанныеПоАдресу = Новый Массив(2);
	мДанныеПоАдресу[0] = ПолучитьТипУлицыИзСправочника(СтрСоединить(мТипУлицы));
	мДанныеПоАдресу[1] = ПолучитьУлицуИзСправочника(СтрСоединить(мАдрес));
	Возврат мДанныеПоАдресу;
КонецФункции

&НаКлиенте
Функция РазделитьТипИИмяПосёлка(ПоселениеСТипом)
	ПоселениеСТипом = СокрЛП(ПоселениеСТипом);
	мДанныеНасселенногоПункта = Новый Массив(2);
	Код = КодСимвола(ПоселениеСТипом, 1);					// Быстра проверка на Крупные города
	Если Код >= 1040 И Код <= 1071 Тогда
		мДанныеНасселенногоПункта[0] = ПолучитьТипНаселенногоПунктаИзСправочника("город");
		мДанныеНасселенногоПункта[1] = ПолучитьНаселенныйПунктИзСправочника(ПоселениеСТипом);
		Возврат мДанныеНасселенногоПункта;
	КонецЕсли;
	
	мПоселение = СтрРазделить(ПоселениеСТипом, " ", Ложь);
	мНаселПункт = Новый Массив();
	мТипНП = Новый Массив();
	// Разделение массива на 2 по первому вхождению заглавной буквы в элементе
	Для Инд = 0 По мПоселение.ВГраница() Цикл
		Код = КодСимвола(мПоселение[Инд], 1);
		Если Код >= 1040 И Код <= 1071 Тогда 				// 1040 - А в Unicode, 1071 - Я
			Для Инд = Инд По мПоселение.ВГраница() Цикл         
				мНаселПункт.Добавить(мПоселение[Инд]);
			КонецЦикла;
			Прервать;
		КонецЕсли;
		мТипНП.Добавить(мПоселение[Инд]);
	КонецЦикла;
	
	мДанныеНасселенногоПункта[0] = ПолучитьТипНаселенногоПунктаИзСправочника(СтрСоединить(мТипНП, " "));
	мДанныеНасселенногоПункта[1] = ПолучитьНаселенныйПунктИзСправочника(СтрСоединить(мНаселПункт, " "));	
	Возврат мДанныеНасселенногоПункта;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтрануИзСправочника(Страна)
	Страна = СокрЛП(Страна);
	Возврат Справочники.СтранаПроживания.НайтиПоНаименованию(Страна);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТипНаселенногоПунктаИзСправочника(Тип)
	Тип = СокрЛП(Тип);
	Возврат Справочники.ТипНаселенногоПункта.НайтиПоРеквизиту("ПолноеНаименование", Тип);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНаселенныйПунктИзСправочника(Поселение)
	Поселение = СокрЛП(Поселение);
	Ссылка = Справочники.НаселенныеПункты.НайтиПоНаименованию(Поселение);
	Если Ссылка.Пустая() Тогда
		Элем = Справочники.НаселенныеПункты.СоздатьЭлемент();
		Элем.Наименование = Поселение;
		Элем.Записать();
		Возврат Элем.Ссылка;
	КонецЕсли;
	Возврат Ссылка;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТипУлицыИзСправочника(ТипУлицы)
	ТипУлицы = СокрЛП(ТипУлицы);
	Возврат Справочники.ТипУлицы.НайтиПоРеквизиту("ПолноеНаименование", ТипУлицы);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьУлицуИзСправочника(Улица)
	Улица = СокрЛП(Улица);
	Ссылка = Справочники.Улицы.НайтиПоНаименованию(Улица);
	Если Ссылка.Пустая() Тогда
		Элем = Справочники.Улицы.СоздатьЭлемент();
		Элем.Наименование = Улица;
		Элем.Записать();
		Возврат Элем.Ссылка;
	КонецЕсли;
	Возврат Ссылка;
КонецФункции

&НаКлиенте
Процедура СохранитьАдрес(Команда)
	Закрыть(Новый Структура("Страна, ТипНаселенногоПункта, НаселенныйПункт, ТипУлицы, Улица, Дом, Корпус, Квартира",
		Страна, ТипНаселенногоПункта, НаселенныйПункт, ТипУлицы, Улица, Дом, Корпус, Квартира));
КонецПроцедуры

#КонецОбласти