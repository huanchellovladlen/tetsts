&НаКлиенте
Процедура АдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
// Работа с Яндекс Картами. Пример запроса:
// https://suggest-maps.yandex.by/suggest-geo?&lang=ru_RU&outformat=json&part=ул Петруся Бровки 8&v=7
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	Если НЕ ЗначениеЗаполнено(Текст) Тогда
		Возврат; 
	КонецЕсли;
	
	Попытка 		// Отпрааляем запрос на определение улицы
		Соединение = Новый HTTPСоединение("suggest-maps.yandex.by",,,,,, Новый ЗащищенноеСоединениеOpenSSL);
		HTTPЗапрос = Новый HTTPЗапрос();	
		HTTPЗапрос.АдресРесурса = "/suggest-geo?&lang=ru_RU&outformat=json&part=" + Текст + "&v=7";
		ОтветHTTP = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
		Если ОтветHTTP.КодСостояния <> 200 Тогда
			Возврат;
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	
	Попытка			// Читаем ответ яндекс карт. Это не API, поэтому мужет сломаться в любой момент.
		ОтветСервера = ОтветHTTP.ПолучитьТелоКакСтроку();
		JSON = Новый ЧтениеJSON;
		JSON.УстановитьСтроку(ОтветСервера);
		jsonОтвет = ПрочитатьJSON(JSON);
		Для Каждого найденныйАдрес ИЗ jsonОтвет.results Цикл
			Если найденныйАдрес.kind = "street" Или найденныйАдрес.kind = "house" Тогда	
				ДанныеВыбора.Добавить(найденныйАдрес.desc + "; " + найденныйАдрес.name);
			КонецЕсли;
		КонецЦикла;
	Исключение
		Возврат;
	КонецПопытки;	
КонецПроцедуры

&НаКлиенте
Процедура АдресV2АвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	// DaData. Вообще, там должен использоваться токен для запросов (10000 бесплатно, а потом берут деньги),
	// но почему-то работает и без него
	// Немного неадекватно работает с адресами Беларуси. Например г.Держинск для него не город
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	Если НЕ ЗначениеЗаполнено(Текст) Тогда
		Возврат; 
	КонецЕсли;
	
	Попытка
		Соединение = Новый HTTPСоединение("suggestions.dadata.ru",,,,,, Новый ЗащищенноеСоединениеOpenSSL);
		HTTPЗапрос = Новый HTTPЗапрос();	
		HTTPЗапрос.АдресРесурса = "/suggestions/api/4_1/rs/suggest/address";
		HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
		HTTPЗапрос.УстановитьТелоИзСтроки("{""query"": """ + Текст + """, ""count"": 4,""locations"":[{""country"": ""*""}]}");
		ОтветHTTP = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
		Если ОтветHTTP.КодСостояния <> 200 Тогда
			Возврат;
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	
	Попытка
		ОтветСервера = ОтветHTTP.ПолучитьТелоКакСтроку();
		JSON = Новый ЧтениеJSON;
		JSON.УстановитьСтроку(ОтветСервера);
		jsonОтвет = ПрочитатьJSON(JSON);
		ВременноеХранилищеАдресов = Новый Массив;
		Для Каждого найденныйАдрес ИЗ jsonОтвет.suggestions Цикл
			ДанныеВыбора.Добавить(найденныйАдрес.unrestricted_value);
		КонецЦикла;
	Исключение
		Возврат;
	КонецПопытки;
КонецПроцедуры